## 输入字符串

AH = 0Ah

DS:DX -> buffer (看上去是数组，实际上是结构)

调用21h号中断就好，其中buffer结构：

> offset	size 	description
>
> 00h		BYTE	maximum characters buffer can hold
>
> 01h		BYTE	(call) number of chars from last input (实际调用中断之后读入字符的个数)
>
> 02h 	N BYTEs	actual characters read, including the final carriage return\
>

所以，建立buffer的时候，代码：

```assembly
data segment
buffer	db 25; 输入的max（最多输入25个字符）
		db ?; 调用后这个位置会存上长度（不包括回车）
		db 25 dup(?); 但实际上也会把回车放进去，所以最多只能输入24位（最后是回车位）
data ends
```

或直接：

```assembly
data segment
	buf db 80, ?, 8 dup(0)
data ends
```

## 乘法

```assembly
mul ebx; EDX:EAX=乘积(eax*ebx)
mul bx; DX:AX=乘积(ax*bx)
mul bh; ax=乘积(al*bl)
```

**被乘数一定都是eax/ax/al**

## 杂

dosbox支持的文件名不超过8位，超过8位就不能用...mdzz

比如要`masm abcdefghijk;`，需要`masm abcdef~1;`

## 交换指令

`xchg op1, op2` 交换(exchange)指令，寄存器和寄存器交换：

```assembly
mov ax, 1
mov bx, 2
xchg ax, bx; AX = 2, BX = 1
```

寄存器和变量交换：

```assembly
mov word ptr ds:[bx], 1234h
mov ax, 5678h
xchg ax, word ptr ds:[bx]; ax = 1234h, word ptr ds:[bx]这个变量变为5678h
```

**但绝对不允许两个变量交换**

## 输入输出指令

IN, OUT 端口（port），用于外部设备的输入

`in al, 60h`:读取用户的键盘输入（从60端口读入，全球统一从60读）

端口地址的最小值为0，最大值为FFFF

**cmos相关的两个端口：70h，71h**：时钟

```assembly
mov al, 0
out 70h, al; 通知cmos之后要访问0号地址
in al, 71h; 读取cmos中的内容，AL=cmos中0号地址中的值
```

上述三句也可以写为：

```assembly
mov al, 0
mov dx, 70h
out dx, al
mov dx, 71h
in al, dx; 可以用dx表示端口地址，但也只能用DX表示
```

第二种可以用来扫描一定范围内的端口，另外，当端口地址大于等于100h时，只能用`in al, dx`这样的dx来表示，不能用常数

```assembly
data segment
current_time db "00:00:00", 0Dh, 0Ah, "$"
data ends
code segment
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   mov al, 4
   out 70h,al; index hour
   in al,71h ; AL=hour(e.g. 19h means 19 pm.)
   call convert
   			;mov word ptr current_time[0],ax
   mov current_time[0], al
   mov current_time[1], ah; 这里可以用 mov word ptr current_time[0], ax 代替两句话
   mov al,2
   out 70h,al; index minute
   in  al,71h; AL=minute
   call convert
   mov word ptr current_time[3],ax;
   ;mov current_time[3], al
   ;mov current_time[4], ah
   mov al,0  ; index second
   out 70h,al
   in  al,71h; AL=second
   call convert
   mov word ptr current_time[6],ax
   mov ah, 9
   mov dx, offset current_time
   int 21h
   mov ah, 4Ch
   int 21h
;---------Convert----------------
;Input:AL=hour or minute or second
;      format:e.g. hour   15h means 3 pm.
;                  second 56h means 56s
;Output: (e.g. AL=56h)
;     AL='5'
;     AH='6'
convert:
    push cx
    mov ah,al ; e.g. assume AL=56h
    and ah,0Fh; AH=06h
    mov cl,4
    shr al,cl ; AL=05h
    ; shr:shift right右移
    add ah, '0'; AH='6'
    add al, '0'; AL='5'
    pop  cx
    ret
;---------End of Convert---------
code ends
end main
```



16位汇编语言中可以调用的资源：

(1) int 21h DOS 中断

```assembly
mov ah, 1
int 21h; AL = getchar();
```

(2) bios 调用（中间层，上面的调用也要转化为这步，之后转成端口调用）

```assembly
mov ah, 0
int 16h; AX = 键盘编码
```

(3) 端口（最底层，最本源，上面两种调用最底层都是这个）

```assembly
in al, 60h
```

## 奇技淫巧

不知道有什么用的music程序，不调用音响

```assembly
NOTE_1  =  440 ; 音调频率
NOTE_2  =  495
NOTE_3  =  550
NOTE_4  =  587
NOTE_5  =  660
NOTE_6  =  733
NOTE_7  =  825

ONE_BEEP  =  600 ; 一拍延时600ms
HALF_BEEP =  300 ; 半拍延时300ms

data segment
ticks dw 0
music dw  NOTE_5, ONE_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_5, HALF_BEEP
dw  NOTE_1*2, ONE_BEEP*2
dw  NOTE_6, ONE_BEEP
dw  NOTE_1*2, ONE_BEEP
dw  NOTE_5, ONE_BEEP*2
dw  NOTE_5, ONE_BEEP
dw  NOTE_1, HALF_BEEP
dw  NOTE_2, HALF_BEEP
dw  NOTE_3, ONE_BEEP
dw  NOTE_2, HALF_BEEP
dw  NOTE_1, HALF_BEEP
dw  NOTE_2, ONE_BEEP*4
dw  NOTE_5, ONE_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_5, HALF_BEEP
dw  NOTE_1*2, HALF_BEEP*3
dw  NOTE_7, HALF_BEEP
dw  NOTE_6, ONE_BEEP
dw  NOTE_1*2, ONE_BEEP
dw  NOTE_5, ONE_BEEP*2
dw  NOTE_5, ONE_BEEP
dw  NOTE_2, HALF_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_4, HALF_BEEP*3
dw  NOTE_7/2, HALF_BEEP
dw  NOTE_1, ONE_BEEP*4
dw  NOTE_6, ONE_BEEP
dw  NOTE_1*2, ONE_BEEP
dw  NOTE_1*2, ONE_BEEP*2
dw  NOTE_7, ONE_BEEP
dw  NOTE_6, HALF_BEEP
dw  NOTE_7, HALF_BEEP
dw  NOTE_1*2, ONE_BEEP*2
dw  NOTE_6, HALF_BEEP
dw  NOTE_7, HALF_BEEP
dw  NOTE_1*2, HALF_BEEP
dw  NOTE_6, HALF_BEEP
dw  NOTE_6, HALF_BEEP
dw  NOTE_5, HALF_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_1, HALF_BEEP
dw  NOTE_2, ONE_BEEP*4
dw  NOTE_5, ONE_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_5, HALF_BEEP
dw  NOTE_1*2, HALF_BEEP*3
dw  NOTE_7, HALF_BEEP
dw  NOTE_6, ONE_BEEP
dw  NOTE_1*2, ONE_BEEP
dw  NOTE_5, ONE_BEEP*2
dw  NOTE_5, ONE_BEEP
dw  NOTE_2, HALF_BEEP
dw  NOTE_3, HALF_BEEP
dw  NOTE_4, HALF_BEEP*3
dw  NOTE_7/2, HALF_BEEP
dw  NOTE_1, ONE_BEEP*3
dw  0, 0
data ends

code segment
assume cs:code, ds:data, ss:stk
main:
   mov ax, data
   mov ds, ax
   xor ax, ax
   mov es, ax
   mov bx, 8*4
   mov ax, es:[bx]
   mov dx, es:[bx+2]   ; 取int 8h的中断向量
   mov cs:old_int8h[0], ax
   mov cs:old_int8h[2], dx; 保存int 8h的中断向量
   cli
   mov word ptr es:[bx], offset int_8h
   mov es:[bx+2], cs   ; 修改int 8h的中断向量
   mov al, 36h
   out 43h, al
   mov dx, 0012h
   mov ax, 34DCh       ; DX:AX=1193180
   mov cx, 1000
   div cx              ; AX=1193180/1000
   out 40h, al
   mov al, ah
   out 40h, al         ; 设置时钟振荡频率为1000次/秒
   sti
   mov si, offset music
   cld
again:
   lodsw
   test ax, ax
   jz done
   call frequency
   lodsw
   call delay
   jmp again
done:
   cli
   mov ax, cs:old_int8h[0]
   mov dx, cs:old_int8h[2]
   mov es:[bx], ax
   mov es:[bx+2], dx   ; 恢复int 8h的中断向量
   mov al, 36h
   out 43h, al
   mov al, 0
   out 40h, al
   mov al, 0
   out 40h, al         ; 恢复时钟振荡频率为1193180/65536=18.2次/秒
   sti
   mov ah, 4Ch
   int 21h

frequency:
   push cx
   push dx
   mov cx, ax   ; CX=frequency
   mov dx, 0012h
   mov ax, 34DCh; DX:AX=1193180
   div cx       ; AX=1193180/frequency
   pop dx
   pop cx
   cli
   push ax
   mov al, 0B6h
   out 43h, al
   pop ax
   out 42h, al ; n的低8位
   mov al, ah
   out 42h, al ; n的高8位 
               ; 每隔n个tick产生一次振荡
               ; 振荡频率=1193180/n (次/秒)
   sti
   ret

delay:
   push ax
   cli
   in al, 61h
   or al, 3
   out 61h, al; 开喇叭
   sti
   pop ax
   mov [ticks], ax
wait_this_delay:
   cmp [ticks], 0
   jne wait_this_delay
   cli
   in al, 61h
   and al, not 3
   out 61h, al; 关喇叭
   sti
   ret

int_8h:
   push ax
   push ds
   mov ax, data
   mov ds, ax
   cmp [ticks], 0
   je skip
   dec [ticks]
skip:
   pop ds
   pop ax
   jmp dword ptr cs:[old_int8h]
old_int8h dw 0, 0
code ends

stk segment stack
dw 100h dup(0)
stk ends
end main
```



## 地址传送指令

### LEA

LEA: 载入当前地址（load effective address）

理解为：取变量的偏移地址

```assembly
lea dx, ds:[bx+si+3]; dx = bx+si+3
```

上述语句，不能`mov dx, bx+si+3`，这是语法错误，体现lea的优越性，不然只能：

```assembly
mov dx, bx
add dx, si
add dx, 3
```

可以有很多巧妙的用法，比如在gcc中会把乘法优化为：

```assembly
lea eax, [eax+4*eax]; EAX=EAX*5
lea eax, [eax+eax*2]; EAX=EAX*3 (好久以前逆程序的时候看到过...当时很崩溃...666)
```

### 远指针

远指针：包括段地址及偏移地址两个部分

近指针：只包括偏移地址，不包含段地址

## LDS

内存布局：

> 1000:0000 78h
>
> 1000:0001 56h
>
> 1000:0002 34h
>
> 1000:0003 12h

假定要把1000：0000中存放的远指针(1234:5678)取出来，存放到ds:bx中，则：

```assembly
mov ax, 1000h
mov ds, ax
lds bx, dword ptrds:[0000h]; ds = 1234h, bx = 5678h
```

不然要：

```assembly
mov ax, 1000h
mov ds, ax
mov bx, word ptr ds:[0]
mov ds, word ptr ds:[2]
```









